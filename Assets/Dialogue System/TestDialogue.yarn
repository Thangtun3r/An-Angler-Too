title:Setup
position: 234,-374
---
//Bob scenario
<<declare $StarringWheel = false>>

//King in Yellow scenario
<<declare $VinEel = false>>
<<declare $TurnedBlue = false>>

//Kitty scenario
<<declare $OnRug = false>>

//BSOD scenario
<<declare $JellyElectric = false>>

//ShopKeeper
<<declare $LaidCat = false>>
<<declare $LegendaryRod = false>>

//
===

title: Start
when: once
---
YOU ARE AN ANGLER. LEFT CLICK ON THE ARROW BELOW TO PROGRESS DIALOGUE.
WHEN NOT IN DIALOGUE, LEFT CLICK TO CAST YOUR LINE
DIALOGUE WILL END AFTER THIS, CATCH A FISH
===


//_______________________________________________


title:Bob
when: once
position: 106,-88
---
YOU: Hello good sir, rumour has brought me here searching for the LEGENDARY fish, you must know where it is?
BOB: The Fish? Ah yes, the shopkeeper has an idea. I can lead you to him, but first I need your help finding a new steering wheel.
YOU: Sure, I can help you with tha-
-> Sure, I can help you find it.
    BOB: Great! Just cast your line at the billboard behind me and reel in a fresh steering wheel.
        <<jump Tutorial>>
-> Im an angler, I don't know where to find steering wheels.
    BOB: Silly boy... Just cast your line at the billboard behind me and fish out a fresh steering wheel.
        <<jump Tutorial>>
===

title:Tutorial
position: 107,179
---
YOU: Fish it our from a billboard? That sounds... ridiculous.
BOB: A fisher man can fish from anything BLUE! The billboard is BLUE, just give it a try.
YOU: Alright... I'll try?
BOB: Also try pressing TAB after this dialogue ends.
===

title:Bob
when: always
position: 389,-87
---
<<checkForItem Bob StarringWheel>>
BOB: Did you do it? You can press TAB to check your inventory.

<<if $StarringWheel>>
-> This starfish is the steering wheel?
    BOB: Great work! Keep it up young man, keep casting your line into any and all things that is BLUE! You'll be reeling in the LEGENDARY fish in no time!
    YOU: Wait, where are you going? Where's the shopkeeper?
        <<bobFlyAway Bob>>

<<else>>
    <<jump End>>
<<endif>>
===

title:End
position: 392,178
---
YOU: I'll get to it...
BOB: Okay good luck.
===


//____________________________________________


title: VinylPlayer
when: once
---
YOU: Hi.
VINYL PLAYER: ...
YOU: Hello!
VINYL PLAYER: Why are you talking to a Vinyl Player?
-> There's a quest marker on you.
    VINYL PLAYER: Peculiar... Fortunately, I do have a favor to ask.
    VINYL PLAYER: I want to make the big guy there feel BLUE.
        <<jump FeelingBlue>>
-> I wanted to change the music.
    VINYL PLAYER: You'll need vinyl records for that, go find some.
    VINYL PLAYER: Before you go I have a favor to ask. Find me a really sad song.
    VINYL PLAYER: I want to make the big guy there feel BLUE.
        <<jump FeelingBlue>>
===

title: FeelingBlue
---
YOU: Feel BLUE? As in feel sad?
    VINYL PLAYER: Exactly! Bring me a vinyl record with a really sad song, I'll play it our loud.
===

title: VinylPlayer
when: always
---
<<checkForItem VinylPlayer VinEel>>
VINYL PLAYER: Found the sad song yet?

<<if $VinEel>>
-> I found a vin-eel disk.
    VINYL PLAYER: Perfect? Put it on me and let's see if it works.
        <<set $TurnedBlue = true>>
        <<changeMaterial KingInYellow>>

<<else>>
YOU: Not yet.
VINYL PLAYER: Well, get going.
<<endif>>
===

title: VinylPlayer
when: $TurnedBlue == true
---
VINYL PLAYER: Cry me a river, hehe.
===

//____________________________________________


title: Kitty
when: always
---
KITTY: Mrrr... Meow (I love laying in the sun)
-> [Pick up] the cat
    <<toggleCat Player>>
-> Pet the cat
    KITTY: Mrrrrrrrrrrrrrrrrrrrrrrrrrr...
===

title: PutDown
---
-> Lay cat on the rug <<if $OnRug>>
    KITTY: Prrrrrr... (I love you)
    <<toggleRugCat Player>>
    <<set $LaidCat = true>>
-> Put the cat down. <<if $OnRug == false>>
    KITTY: [Kitty hops back on you]
    KITTY: Ks! (Hey! Only put me down somewhere indoor, soft and bright.)
-> Pet the cat.
    KITTY: Aur... Mrrrrr...
-> Teleport the cat back to where it was
    KITTY: OOOooo..
        <<toggleCat Player>>
===

title: KittyOnRug
---
It's sleeping... And BLUE!
===


//____________________________________________


title: Puter
when: once
---
PUTER: I've been rendering videos for 10 days boss...
PUTER: You have to crash the system, let me rest.
YOU: Leave it to me, brother.
===

title: Puter
when: always
---
<<checkForItem Puter JellyElectric>>
PUTER: Ohh... The headache...

<<if $JellyElectric>>
-> I'll short the system. Good night, brother.
    <<changeMaterial Screen>>
    <<changeMaterial Puter>>

<<else>>
YOU: Hang on, brother.
<<endif>>
===

//______________________________________________



